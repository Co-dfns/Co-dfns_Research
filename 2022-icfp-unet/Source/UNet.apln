⍝ Copyright (c) 2022 Aaron W. Hsu. <arcfide@sacrideo.us>

:Namespace UNET

 W←⍬ ⋄ V←⍬ ⋄ Z←⍬ ⋄ LR←1e¯9 ⋄ MO←0.99

 FWD←{Z⊢←⍬⍴⍨≢W
  CV←{0⌈z⊣Z[⍺]←⊂Z[⍺],⊂z←(,[2+⍳3]1 1↓¯1 ¯1↓{⍵}⌺3 3⊃Z[⍺]←⊂⍵)+.×,[⍳3]⍺⊃W}
  CC←{⍵,⍨(⌊p)↓(-⌈p)↓(⍺⊃Z)⊣p←2÷⍨(⍴⍺⊃Z)-⍴⍵}
  MX←{⌈⌿[2],[2 3]{⍵}⌺(2 2⍴2)⊃Z[⍺]←⊂⍵}
  UP←{((2×¯1↓⍴⍵),¯1↑⍴⍺⊃W)⍴0 2 1 3 4⍉⍵+.×⍺⊃W⊣Z[⍺]←⊂⍵}
  C1←{1E¯8+z÷[⍳2]+/z←*z-[⍳2]⌈/z←⍵+.×⍺⊃W⊣Z[⍺]←⊂⍵}
  LA←{⍺≥≢Z:⍵ ⋄ (⍺+2)CC(⍺+5)UP(⍺+4)CV(⍺+3)CV(⍺+6)∇(⍺+2)MX(⍺+1)CV(⍺+0)CV ⍵}
  2 C1 1 CV 0 CV 3 LA ⍵⍴⍨3↑1,⍨⍴⍵}

 BCK←{Y←⍺ ⋄ Y∆←⍵ ⋄ ∆←{0⊣W[⍺]-←LR×V[⍺]←(⊂⍵)+MO×V[⍺]}
  ∆CV←{w←,[⍳3]⊖⌽[1]0 1 3 2⍉⍺⊃W ⋄ x←⊃⍺⊃Z ⋄ ∆z←⍵×0<1⊃⍺⊃Z ⋄ ∆Z←0⍪0⍪⍨0,[1]∆z,[1]0
   _←⍺ ∆ 3 0 1 2⍉(⍉,[⍳2]∆z)+.×,[⍳2]1 1↓¯1 ¯1↓{⍵}⌺3 3⊢x ⋄ w+.×⍨,[2+⍳3]{⍵}⌺3 3⊢∆Z}
  ∆CC←{x←⍺⊃Z ⋄ ∆z←⍵ ⋄ d←-⌊2÷⍨2↑(⍴x)-⍴∆z ⋄ (⊃d)⊖(1⊃d)⌽[1](⍴x)↑∆z}
  ∆MX←{x←⍺⊃Z ⋄ ∆z←⍵ ⋄ y×x=y←(⍴x)↑2⌿2/[1]∆z}
  ∆UP←{w←⍺⊃W ⋄ x←⍺⊃Z ⋄ ∆z←⍵ ⋄ cz←(2 2⍴2)SF ∆z
   _←⍺ ∆(⍉,[⍳2]x)+.×,[⍳2]cz ⋄ (,[2+⍳3]cz)+.×⍉⍪w}
  ∆C1←{w←⍺⊃W ⋄ x←⍺⊃Z ⋄ ∆z←⍵ ⋄ _←⍺ ∆(⍉,[⍳2]x)+.×,[⍳2]∆z ⋄ ∆z+.×⍉w}
  ∆LA←{⍺≥≢Z:⍵ ⋄ (⍺+0)∆CV(⍺+1)∆CV(⍵ ∆CC⍨⍺+2)+(⍺+2)∆MX(⍺+6)∇(⍺+3)∆CV(⍺+4)∆CV(⍺+5)∆UP ⍵↑[2]⍨-2÷⍨⊃⌽⍴⍵}
  3 ∆LA 0 ∆CV 1 ∆CV 2 ∆C1 Y∆-(~Y),[1.5]Y}

 E←{-+⌿,⍟(⍺×⍵[;;1])+(~⍺)×⍵[;;0]}

 RUN←{Y Y∆(Y E Y∆)⊣(Y←⌊0.5+nm↑⍵↓⍨2÷⍨(⍴⍵)-nm←2↑⍴Y∆)BCK⊢Y∆←FWD ⍺}

 K←{⍺←⊢ ⋄ I B S←⍺⊣1 64 2 ⋄ D←⍵ 
  FD←×⍀I B,(D⍴S),÷S ⋄ KS←2/⍪3 3 0 3 3 2 ⋄ LM←0 1 1 1 1 1 3 2 2 2 2 1
  N←{0=×⌿⍵:⍵⍴0 ⋄ (0.5*⍨2÷×⌿1↓⍵)×(0.5*⍨¯2×⍟?⍵⍴0)×1○○2×?⍵⍴0}
  W←N¨¨({↓KS,6 2⍴FD[LM+⍵]}⍤0⍳D)((3 3)(3 3)⍬,¨↓3 2⍴FD[2,4⍴1],2)
  W⊣(⊃W)←⍉1 2 0 3∘⍉¨@5⍉⊃W}

 TRAIN←{⍺←100 ⋄ iter←⍺ ⋄ data←OPEN ⍵ ⋄ RUN←⍺⍺ ⋄ W{(,1⊃⍵),,0⊃⍵}←#.WEIGHTS ⋄ V⊢←⍬⍴⍨≢W
  update←{y ∆ e←⍵ ⋄ #.REFERENCE←y ⋄ #.MASK←∆ ⋄ #.ERRORS⍪←(1+⊃⊖#.ERRORS),⎕←e ⋄ 0}
  _←{update ⊃RUN⌿⎕FREAD data ⍵}¨∊iter⍴{1+⍳⍵-⍺}⌿2↑⎕FSIZE data
  0⊣⎕FUNTIE data}

 OPEN←{3::'OPEN PATH MISMATCH'⎕SIGNAL 22 ⋄ 24::⎕FNUMS[⎕FNAMES⍳⍵] ⋄ ⍵ ⎕FTIE 0}

 CONVERT←{fns2imgs←⍺⍺ ⋄ files←⍵⍵ ⋄ out←⍺ ⋄ in←⍵
  _←0⊣⎕FUNTIE tie⊣⎕FAPPEND∘tie∘fns2imgs⍤1⊢files in⊣tie←out ⎕FCREATE⍠'Z' 1⊢0}

 PNGS2IMGS←{_←#.UNET_cd.∆.Init ⋄ (256÷⍨+⌿÷≢)∘#.UNET_cd.∆.LoadImage¨⊆⍵}

 ISBI∆FILES←{ls←{⊃⎕NINFO⍠1⊢⍵} ⋄ (ls ⍵,'\images\*.png'),⍪ls ⍵,'\labels\*.png'}

 ∇PLOT
  #.UNET_cd.∆.Init
  #.UNET_cd.∆.{⎕←'Stopped plot.'⊣{_←⎕DL .5 ⋄ ⍺ Plot #.ERRORS}Display{#.STOP}⍬}&⍬
  #.UNET_cd.∆.{⎕←'Stopped reference.'⊣{_←⎕DL .5 ⋄ ⍺ Image #.REFERENCE}Display{#.STOP}⍬}&⍬
  #.UNET_cd.∆.{⎕←'Stopped mask.'⊣{_←⎕DL .5 ⋄ ⍺ Image #.MASK}Display{#.STOP}⍬}&⍬
 ∇

 ∇INIT
  #.STOP←0
  #.ERRORS←⍉⍪0 15000
  #.WEIGHTS←K 4
 ∇

 time←{                                  ⍝ Time function application.         
   tfmt←{                              ⍝ elapsed time format hh:mm:ss.cc      
     csec←{⌊0.5+0.1×⍵-⍺}             ⍝ elapsed centi-seconds.                 
     tvec←{0 60 60 100⊤⍵}            ⍝ time vector: hours mins secs centi.    
     pair←{(-2⌈⍴⍵)↑'0',⍵}∘⍕          ⍝ 2-digit time unit.                     
     sepr←{1↓↑,/':::.',¨⍵}           ⍝ ':' or '.' separated pairs.            
     trim←{(~{⌽∨\⌽⍵}¯2⌽'00:'⍷⍵)/⍵}   ⍝ leading 00:s removed.                  
     trim sepr pair¨tvec ⍺ csec ⍵    ⍝ hh:mm:ss.cc                            
   }                                                                          
   0::(,↑⍬⍴⎕DM)⎕SIGNAL ⎕EN             ⍝ catchall: pass back error.           
   fm←⍬⍴2↓⎕AI                          ⍝ start time.                          
   6::{}⎕←fm tfmt ⍬⍴2↓⎕AI              ⍝ value error: fn didn't return result.
   ⍺←⊢ ⋄ rslt←⍺ ⍺⍺ ⍵                   ⍝ apply function.                      
   ⎕←fm tfmt ⍬⍴2↓⎕AI                   ⍝ display elapsed time.                
   1:shy←rslt                          ⍝ pass on shy result.                  
 }                                                                            

:EndNamespace
