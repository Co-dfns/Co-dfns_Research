:Namespace UNET_aux

 CV5←{K←⍺ ⋄ M←⍵ ⋄ Z←0⍴⍨(S←¯2+2↑⍴M),¯1↑⍴K ⋄ Ks←⍴K
  Z⊣(⍳Ks[0])∘.{0⊣Z+←(S↑⍺ ⍵↓M)+.×K[⍺;⍵;;]}⍳Ks[1]}

 SF1←{R←≢W←⍺ ⋄ A←⍵ ⋄ S←1+(R↑⍴A)-W ⋄ ST←(×⌿S),×⌿Ts←R↓⍴A
  (S,W,Ts)⍴1 0 2⍉{ST⍴S↑⍵↓A}⍤1⊢⍉W⊤⍳×⌿W}

 SF2←{R←≢W←0⌷⍺ ⋄ S←1⌷⍺ ⋄ A←⍵ ⋄ P←⌈S÷⍨(R↑⍴A)-W-1 ⋄ ST←(×⌿P),×⌿Ts←R↓⍴A
  I←S×⍳¨P ⋄ (P,W,Ts)⍴1 0 2⍉{ST⍴(I+⍵)⌷A}⍤1⊢⍉W⊤⍳×⌿W}

 SW←{R←≢W←0⌷⍺ ⋄ S←1⌷⍺ ⋄ A←⍵ ⋄ P←⌈S÷⍨(R↑⍴A)-W-1 ⋄ Ts←R↓⍴A
  (I,(⍳≢Ts)+≢I←,I,⍪R+I←⍳R)⍉(Ts,⍨,P,⍪S)⍴(P×S)↑⍵}

 SF←{1≥≢⍴⍺:⍺ SF1 ⍵ ⋄ ⍺ SW ⍵ ⋄ ⍺ SF2 ⍵}

 CV2←{(,[2 3 4]3 3 SF ⍵)+.×,[0 1 2]⍺}

 RelU←{0⌈⍵}
 CV←{(,[2 3 4]3 3 SF ⍵)+.×⍺}
 CC←{⍵,⍨(⌊p)↓(-⌈p)↓⍺⊣p←2÷⍨(⍴⍺)-⍴⍵}
 MX←{⌈⌿[2],[2 3](2 2⍴2)SF ⍵}
 UP←{((2×¯1↓⍴⍵),¯1↑⍴⍺)⍴0 2 1 3 4⍉⍵+.×⍺}
 C1←{1E¯8+z÷[0 1]+/z←*z-[0 1]⌈/z←⍵+.×⍺}

 ∆CV←{w←⌽⊖[2]1 0 2 3⍉⊃⍺ ⋄ x←⊃1⊃⍺ ⋄ ∆z←⍵×0<1⊃1⊃⍺ ⋄ ∆Z←¯2⊖¯2⌽[1](4+2↑⍴∆z)↑∆z
  (0 3 2 1⍉(⍉,[0 1]∆z)+.×,[0 1]3 3 SF x)((,[2 3 4] 3 3 SF ∆Z)+.×⍉⍪w)}
 ∆CC←{x←1⊃⍺ ⋄ ∆z←⍵ ⋄ d←-⌊2÷⍨2↑(⍴x)-⍴∆z ⋄ (⊃d)⊖(1⊃d)⌽[1](⍴x)↑∆z}
 ∆MX←{x←1⊃⍺ ⋄ ∆z←⍵ ⋄ y×x=y←(⍴x)↑2⌿2/[1]∆z}
 ∆UP←{w←3 0 1 2⍉⊃⍺ ⋄ x←⊃1⊃⍺ ⋄ ∆z←⍵
  (1 3 2 0⍉(⍉,[0 1]x)+.×,[0 1](2 2⍴2)SF ∆z)((,[2 3 4]⊢(2 2⍴2)SF ∆z)+.×⍉⍪w)}
 ∆C1←{w←⊃⍺ ⋄ x←1⊃⍺ ⋄ ∆z←⍵ ⋄ ((⍪⍉∆z)+.×⍉⍪⍉x)(∆z+.×w)}

 E←{-+⌿,⍟(⍺×⍵[;;1])+(~⍺)×⍵[;;0]}

 mk_Y←{⌊0.5+nm↑⍺↓⍨2÷⍨(⍴⍺)-nm←2↑⍴⍵}

:EndNamespace