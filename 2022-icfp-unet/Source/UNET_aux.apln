⍝ Copyright (c) 2022 Aaron W. Hsu, <arcfide@sacrideo.us>

:Namespace UNET_aux

 W←⍬ ⋄ V←⍬ ⋄ Z←⍬ ⋄ LR←1e¯9 ⋄ MO←0.99

 FWD←{Z⊢←(≢W)⍴⊂⍬
  CV←{0⌈z⊣Z[⍺]←⊂Z[⍺],⊂z←(,[2+⍳3]3 3 SF⊃Z[⍺]←⊂⍵)+.×,[⍳3]⍺⊃W}
  CC←{⍵,⍨(⌊p)↓(-⌈p)↓(⍺⊃Z)⊣p←2÷⍨(⍴⍺⊃Z)-⍴⍵}
  MX←{⌈⌿[2],[2 3](2 2⍴2)SF⊃Z[⍺]←⊂⍵}
  UP←{((2×¯1↓⍴⍵),¯1↑⍴⍺⊃W)⍴0 2 1 3 4⍉⍵+.×⍺⊃W⊣Z[⍺]←⊂⍵}
  C1←{1E¯8+z÷[⍳2]+/z←*z-[⍳2]⌈/z←⍵+.×⍺⊃W⊣Z[⍺]←⊂⍵}
  LA←{⍺≥≢Z:⍵ ⋄ (⍺+2)CC(⍺+5)UP(⍺+4)CV(⍺+3)CV(⍺+6)∇(⍺+2)MX(⍺+1)CV(⍺+0)CV ⍵}
  2 C1 1 CV 0 CV 3 LA ⍵⍴⍨3↑1,⍨⍴⍵}

 BCK←{Y←⍺ ⋄ Y∆←⍵ ⋄ ∆←{0⊣W[⍺]←⊂(⍺⊃W)-LR×⊃V[⍺]←⊂⍵+MO×(⍴⍵)⍴⍺⊃V}
  ∆CV←{w←,[⍳3]⊖⌽[1]0 1 3 2⍉⍺⊃W ⋄ x←⊃⍺⊃Z ⋄ ∆z←⍵×0<1⊃⍺⊃Z ⋄ ∆Z←¯2⊖¯2⌽[1](4+2↑⍴∆z)↑∆z
   _←⍺ ∆ 3 0 1 2⍉(⍉,[⍳2]∆z)+.×,[⍳2]3 3 SF x ⋄ w+.×⍨,[2+⍳3]3 3 SF ∆Z}
  ∆CC←{x←⍺⊃Z ⋄ ∆z←⍵ ⋄ d←-⌊2÷⍨2↑(⍴x)-⍴∆z ⋄ (⊃d)⊖(1⊃d)⌽[1](⍴x)↑∆z}
  ∆MX←{x←⍺⊃Z ⋄ ∆z←⍵ ⋄ y×x=y←(⍴x)↑2⌿2/[1]∆z}
  ∆UP←{w←⍺⊃W ⋄ x←⍺⊃Z ⋄ ∆z←⍵ ⋄ cz←(2 2⍴2)SF ∆z
   _←⍺ ∆(⍉,[⍳2]x)+.×,[⍳2]cz ⋄ (,[2+⍳3]cz)+.×⍉⍪w}
  ∆C1←{w←⍺⊃W ⋄ x←⍺⊃Z ⋄ ∆z←⍵ ⋄ _←⍺ ∆(⍉,[⍳2]x)+.×,[⍳2]∆z ⋄ ∆z+.×⍉w}
  ∆LA←{⍺≥≢Z:⍵ ⋄ (⍺+0)∆CV(⍺+1)∆CV(⍵ ∆CC⍨⍺+2)+(⍺+2)∆MX(⍺+6)∇(⍺+3)∆CV(⍺+4)∆CV(⍺+5)∆UP ⍵↑[2]⍨-2÷⍨⊃⌽⍴⍵}
  3 ∆LA 0 ∆CV 1 ∆CV 2 ∆C1 Y∆-(~Y),[1.5]Y}

 E←{-+⌿,⍟(⍺×⍵[;;1])+(~⍺)×⍵[;;0]}

 RUN←{Y Y∆(Y E Y∆)⊣(Y←⌊0.5+nm↑⍵↓⍨2÷⍨(⍴⍵)-nm←2↑⍴Y∆)BCK⊢Y∆←FWD ⍺}

 init_wv←{W⊢←(,1⊃⍵),,0⊃⍵ ⋄ V⊢←(≢W)⍴⊂⍬ ⋄ ≢W} ⋄ get_w←{W}

 SF1←{R←≢W←⍺ ⋄ A←⍵ ⋄ S←1+(R↑⍴A)-W ⋄ ST←(×⌿S),×⌿Ts←R↓⍴A
  (S,W,Ts)⍴1 0 2⍉{ST⍴S↑⍵↓A}⍤1⊢⍉W⊤⍳×⌿W}
 SF2←{R←≢W←0⌷⍺ ⋄ S←1⌷⍺ ⋄ A←⍵ ⋄ P←⌈S÷⍨(R↑⍴A)-W-1 ⋄ ST←(×⌿P),×⌿Ts←R↓⍴A
  I←S×⍳¨P ⋄ (P,W,Ts)⍴1 0 2⍉{ST⍴(I+⍵)⌷A}⍤1⊢⍉W⊤⍳×⌿W}
 SW←{R←≢W←0⌷⍺ ⋄ S←1⌷⍺ ⋄ A←⍵ ⋄ P←⌈S÷⍨(R↑⍴A)-W-1 ⋄ Ts←R↓⍴A
  (I,(⍳≢Ts)+≢I←,I,⍪R+I←⍳R)⍉(Ts,⍨,P,⍪S)⍴(P×S)↑⍵}
 SF←{1≥≢⍴⍺:⍺ SF1 ⍵ ⋄ ⍺ SW ⍵ ⋄ ⍺ SF2 ⍵}

:EndNamespace