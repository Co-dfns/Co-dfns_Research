 Z←e TK ISDFN;READ;SEQ;DF;ER;ET;EX;IF;KW;NL;NM;PM;RB;ST;ET∆;EX∆;IF∆;KW∆;NL∆;RB∆;stk;t;v;p;xt;xv;xp;XDF;XNM;XPM
⍝ PARAMETERS
⍝  idx:     Current reader position
⍝  IN:      Input stream
⍝  TK∆READ: Reader function for running the tokenizer
 ET∆ EX∆ IF∆ KW∆ NL∆ RB∆←⍳6
 XDF XNM XPM←⍳3
 stk←⍬ ⋄ xt xv xp←⍬ ⍬(0 2⍴⍬)
 t v p←0 ⍝ Type | Value | Position
 ⎕FX'Z←READ' 'Z←TK∆READ'
 ⎕FX'DF;ss;se' 'xt,←XDF ⋄ ss←idx ⋄ xv,←⊂PD e ⋄ se←idx ⋄ xp⍪←ss se'
 ⎕FX'ER' '⎕SIGNAL 2'
 ⎕FX'ET' 't←ET∆ ⋄ v←0 ⋄ p←idx,idx'
 ⎕FX'EX' 't←EX∆ ⋄ v e←e PX xt xv xp ⋄ p←(⊃,⊢⌿),xp ⋄ idx-←1 ⋄ xt xv xp←⍬ ⍬(0 2⍴⍬)'
 ⎕FX'IF' 't←IF∆ ⋄ v←0 ⋄ p←idx,idx+1'
 ⎕FX'KW' 't←KW∆ ⋄ v←1⎕C'':'',IN[stk] ⋄ p←(¯1 1+⌊⌿,⌈⌿)stk ⋄ stk←⍬ ⋄ idx-←1'
 ⎕FX'NL' 't←NL∆ ⋄ v←'''' ⋄ p←idx,idx+1'
 ⎕FX'NM' 'xt,←XNM ⋄ xv,←⊂,IN[stk] ⋄ xp⍪←(0 1+⌊⌿,⌈⌿)stk ⋄ stk←⍬'
 ⎕FX'PM' 'xt,←XPM ⋄ xv,←⊂,IN[idx] ⋄ xp⍪←idx,idx+1'
 ⎕FX'RB' 't←RB∆ ⋄ v←0 ⋄ p←idx,idx+1'
 ⎕FX'ST' 'stk⍪←idx'
 TK∆SPEC
 'NON-EMPTY EXPR STACK'⎕SIGNAL 99⌿⍨0≠≢xt
 Z←t v p e
