f∆ N∆←'ptknfsrdx' 'ABEFGLMNOPVZ'
⎕FX∘⍉∘⍪¨'GLM',¨'←{⍪/(0 '∘,¨(⍕¨N∆⍳'GLM'),¨⊂' 0 0,1+⍺),1+@0⍉↑(⊂6⍴⊂⍬),⍵}'
⎕FX∘⍉∘⍪¨'AEFO',¨'←{⍪/(0 '∘,¨(⍕¨N∆⍳'AEFO'),¨⊂' ⍺⍺ 0,1+⍺),1+@0⍉↑(⊂6⍴⊂⍬),⍵}'
⎕FX∘⍉∘⍪¨'BNPVZ',¨'←{0(N∆⍳'''∘,¨'BNPVZ',¨''')'∘,¨'⍺⍺(0⌷⍵)' '0(⍎⍵)' '⍺⍺(⊂⍵)' '⍺⍺(⊂⍵)' '1(⊂⍵)',¨⊂',1+⍺}'
Vt←(⊢⍳⍨0⊃⊣)⊃¯1,⍨1⊃⊣
MkAST←{⍪/(⍳≢⍵)+@0⍉↑⌽⍵}
MkAtom←{∧⌿m←(N∆⍳'N')=⊃¨1⊃¨⍵:⍺(0A)⌽⍵ ⋄ 1=≢⍵:0⊃⍵ ⋄ ⍺(3A)⌽({⊃¨⍵[4 5]}0A⊂)¨@{m}⍵}
MkMget←{⍪/(0,1+2<≢⊃z)+@0⊢z←⍉↑⌽⍵}
Atn←{(0 3⊃⍵)@(⊂3 0)⊢⍺ ⍺⍺ ⍵}
Fn←{a(i d)←⍵ ⋄ 0=≢a:0 ⍬ ⍺(i d)
 0=≢ss←(4⊃z)⌿⍨m←((3=1⊃⊢)∧¯1=2⊃⊢)⊢z←⍪⌿↑a:0(,⊂z)⍺(i d)
 0<c←r⊃⍨0,pi←⊃⍒⊃r←↓⍉↑ps←⍺∘Fa¨ss,¨⊂⊂d:pi⊃ps
 0(,⊂(⊂¨¨z)((⊃⍪⌿)⊣@{m})¨⍨↓(m⌿0⊃z)+@0⍉↑⊃¨1⊃r)⍺(i d)}
FnType←3 3 2 2⊥1+(⊂⊃⍳(,¨'⍵⍵' '⍺⍺','⍺⍵')⍨)⌷1∘⊃,¯1⍨
PEG'Sfn    ← sfn                                              : 1P∘⌽∘∊       '
PEG'Prim   ← prim                                             : 1P           '
PEG'Symbol ← name                                             : ⊢∘⌽          '
PEG'Name   ← Symbol & (⍺⍺=Vt)                                 : ⍺⍺ V∘,∘⊃     '
PEG'Args   ← aaww | aw & (⍺⍺=Vt)                              : ⍺⍺ V∘,∘⊃     '
PEG'Var    ← ⍺⍺ Args | (⍺⍺ Name)                                             '
PEG'Num    ← float | int                                      : N∘⌽          '
PEG'Pex    ← rpar , Ex , lpar                                                '
PEG'Zil    ← zil                                              : 0A           '
PEG'Unit   ← (0 Var) | Num | Zil | Pex                                       '
PEG'Atom   ← Unit+                                            : MkAtom       '
PEG'Semi   ← ∊                                                : ⊣0 P{,'';''} '
PEG'Semx   ← Ex | Semi                                                       '
PEG'Brk    ← rbrk , (Semx , (semi , Semx *)) , lbrk           : 3E∘⌽         '
PEG'Lbrk   ← ∊                                                : ⊣⍺⍺ P{,''[''}'
PEG'Idx    ← Brk , (1 Lbrk) , Atom                            : 2E∘⌽         '
PEG'Slrp   ← ⍺⍺ | (⍵⍵ , ∇) | (⍥ , ∇) ↓                                       '
PEG'Blrp   ← ⍺⍺ , (⍵⍵ Slrp ∇) ↓                                              '
PEG'Bfn    ← rbrc Blrp lbrc                                   : ¯1F          '
PEG'Pfe    ← rpar , Fex , lpar                                               '
PEG'Fnp    ← Prim | Sfn | (1 Var) | Bfn | Pfe                                '
PEG'Pmop   ← mop                                              : 2P           '
PEG'Mop    ← Pmop , Afx                                       : 2O           '
PEG'Pdop1  ← dop1                                             : 2P           '
PEG'Dop1   ← Pdop1 , Afx                                      : 8O∘⌽         '
PEG'Pdop2  ← dop2                                             : 2P           '
PEG'Vop    ← Atom , Pdop2 , Afx                               : 7O∘⌽         '
PEG'Pdop3  ← dop3                                             : 2P           '
PEG'JotDot ← dot , jot                                        : ⊣2O∘,∘⊂2P∘⌽  '
PEG'Dop3a  ← Pdop3 , Atom                                     : 5O∘⌽         '
PEG'Dop3   ← Dop3a | JotDot                                                  '
PEG'Bop    ← rbrk , Ex , lbrk , (2 Lbrk) , Afx                : 7O∘⌽         '
PEG'Fop    ← Fnp , (Dop1 | Dop3 ?)                            : MkAST        '
PEG'Afx    ← Mop | Fop | Vop | Bop                                           '
PEG'Trn    ← Afx , (Afx | Idx | Atom , (∇ ?) ?)               : 3F∘⌽         '
PEG'Bind   ← gets , Symbol [⍺⍺]                               : ⍺⍺ B         '
PEG'Mname  ← 0 Name                                           : 4E Atn       '
PEG'Gets   ← ∊                                                : ⊣⍺⍺ P{,''←''}'
PEG'Ogets  ← 2 Gets                                           : 2O∘⌽         '
PEG'Mbrk   ← Ogets , Brk , (0 Name)                           : 4E∘(1∘↓)Atn∘⌽'
PEG'Mget   ← Afx , (Mname | Mbrk)                             : MkMget       '
PEG'Bget   ← 1 Gets , Brk , (0 Name)                          : 4E∘(1∘↓)Atn∘⌽'
PEG'Asgn   ← gets , (Bget | Mget)                                            '
PEG'Fex    ← Afx , (Trn ?) , (1 Bind *)                       : MkAST        '
PEG'IAx    ← Idx | Atom , (dop2 !)                                           '
PEG'App    ← Afx , (IAx ?)                                    : {⍺((≢⍵)E)⌽⍵} '
PEG'ExHd   ← Asgn | (0 Bind) | App , ∇ ?                                     '
PEG'Ex     ← IAx , ExHd                                       : MkAST        '
PEG'Gex    ← Ex , grd , Ex                                    : G∘⌽          '
PEG'Alp    ← ∊                                                : ''⍺''⍨       '
PEG'Omg    ← ∊                                                : ''⍵''⍨       '
PEG'ClrEnv ← (Alp[¯1]),(Alp,Alp[¯1]),(Omg[¯1]),(Omg,Omg[¯1])↓                '
PEG'Fax    ← lbrc , (Gex | Ex | Fex Stmts rbrc) → Fn          : (FnType ⍺)F  '
PEG'FaFnW  ← Omg[0]↓ , Fax []                                                '
PEG'FaFnA  ← Omg[0] , (Alp[0])↓ , Fax []                                     '
PEG'FaFn   ← FaFnW | FaFnA                                                   '
PEG'FaMopV ← Alp,Alp[0]↓ , FaFn []                                           '
PEG'FaMopF ← Alp,Alp[1]↓ , FaFn []                                           '
PEG'FaMop  ← FaMopV , (FaMopF ?) | FaMopF                                    '
PEG'FaDopV ← Omg,Omg[0]↓ , FaMop []                                          '
PEG'FaDopF ← Omg,Omg[1]↓ , FaMop []                                          '
PEG'FaDop  ← FaDopV , (FaDopF ?) | FaDopF                                    '
PEG'Fa     ← ClrEnv , (FaFn | FaMop | FaDop) []                              '
PEG'Nlrp   ← sep | rbrc ↑ Slrp (lbrc Blrp rbrc)                              '
PEG'Stmt   ← sep | (⍺⍺ , (sep | lbrc) ⌽ Nlrp)                                '
PEG'Stmts  ← ⍵⍵ | (⍺⍺ Stmt , ∇)                                              '
PEG'Ns     ← nss , (Ex | Fex Stmts nse) , eot → Fn             : (¯1+⊣)0F⊢   '
ps←{⍺←⍬ ⍬ ⋄ src←∊{⍵/⍨∧\'⍝'≠⍵}¨⍵,¨⎕UCS 10
 0≠⊃c a e(i d)←p←⍺ Ns 0,⊂src:_report p
 (↓s(-⍳)@3↑⊃a)e(s←∪0(,'⍵')(,'⍺')'⍺⍺' '⍵⍵',3⊃⊃a)src}
